# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//ark/js_runtime/js_runtime_config.gni")
import("//ark/js_runtime/test/test_helper.gni")
import("//ark/ts2abc/ts2panda/ts2abc_config.gni")
import("//build/ohos.gni")
import("//build/test.gni")

test_js_path = "//ark/js_runtime/ecmascript/tooling/test/js/Sample.js"
test_abc_path = "$target_out_dir/Sample.abc"

module_output_path = "ark/js_runtime"

config("debug_api_test") {
  visibility = [ ":*" ]

  cflags_cc = [ "-Wno-gnu-zero-variadic-macro-arguments" ]
  ldflags = [ "-Wl,-rpath=\$ORIGIN/" ]
  include_dirs = [
    "//ark/js_runtime",
    "//ark/js_runtime/ecmascript/tooling/test",
  ]
}

ts2abc_gen_abc("ark_debug_abc") {
  extra_visibility = [ ":*" ]  # Only targets in this file can depend on this.
  src_js = rebase_path(test_js_path)
  dst_file = rebase_path(test_abc_path)
  extra_args = [ "--debug" ]

  in_puts = [ test_js_path ]
  out_puts = [ test_abc_path ]
}

ohos_shared_library("debugger_entry") {
  sources = [ "entry/test_debugger_entry.cpp" ]

  configs = [
    "//ark/js_runtime:ark_jsruntime_public_config",  # should add before
                                                     # arkruntime_public_config
    "//ark/js_runtime:ark_jsruntime_common_config",
    ":debug_api_test",
    "$ark_root/runtime:arkruntime_public_config",
  ]

  deps = [
    ":ark_debug_abc",
    ":jsdebugtest",
    "$ark_root/libpandabase:libarkbase",
    "$ark_root/libpandafile:libarkfile",
  ]

  if (!is_standard_system) {
    deps += [ "$ark_root/runtime:libarkruntime" ]
  }

  output_extension = "so"
  subsystem_name = "test"
}

ohos_shared_library("jsdebugtest") {
  sources = [
    "utils/test_entry.cpp",
    "utils/test_extractor.cpp",
    "utils/test_util.cpp",
    "utils/testcases/test_list.cpp",
  ]

  configs = [
    "//ark/js_runtime:ark_jsruntime_public_config",  # should add before
                                                     # arkruntime_public_config
    "//ark/js_runtime:ark_jsruntime_common_config",
    ":debug_api_test",
    "$ark_root/runtime:arkruntime_public_config",
  ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    "$ark_root/libpandafile:libarkfile",
    "//ark/js_runtime:libark_jsruntime_test_static",
    "//ark/js_runtime/ecmascript/tooling:libark_ecma_debugger_test_static",
  ]

  if (!is_linux) {
    external_deps = [ "bytrace_standard:bytrace_core" ]
  }

  if (is_standard_system) {
    deps += [ "$ark_root/runtime:libarkruntime_static" ]
  } else {
    deps += [ "$ark_root/runtime:libarkruntime" ]
  }

  output_extension = "so"
  subsystem_name = "test"
}

ohos_unittest("EcmaDebugApiTest") {
  module_out_path = module_output_path

  sources = [
    # test file
    "debugger_api_test.cpp",
  ]

  defines = [ "DEBUGGER_TEST_LIBRARY=\"libdebugger_entry.so\"" ]

  configs = [
    "//ark/js_runtime:ark_jsruntime_public_config",  # should add before
                                                     # arkruntime_public_config
    "//ark/js_runtime:ark_jsruntime_common_config",
    ":debug_api_test",
    "$ark_root/runtime:arkruntime_public_config",
  ]

  deps = [
    ":jsdebugtest",
    "$ark_root/libpandabase:libarkbase",
    "$ark_root/libpandafile:libarkfile",
  ]

  if (!is_standard_system) {
    deps += [ "$ark_root/runtime:libarkruntime" ]
  }
}

host_unittest_action("DebuggerCommandsTest") {
  module_out_path = module_output_path

  sources = [
    # test file
    "debugger_commands_test.cpp",
  ]

  configs = [
    "//ark/js_runtime:ecma_test_config",
    "//ark/js_runtime:ark_jsruntime_public_config",  # should add before
                                                     # arkruntime_public_config
    "//ark/js_runtime:ark_jsruntime_common_config",
    "$ark_root/runtime:arkruntime_public_config",
  ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    "//ark/js_runtime/ecmascript/tooling:libark_ecma_debugger_test",
    sdk_libc_secshared_dep,
  ]

  if (!is_standard_system) {
    deps += [ "$ark_root/runtime:libarkruntime" ]
  }
}

host_unittest_action("DebuggerEventsTest") {
  module_out_path = module_output_path

  sources = [
    # test file
    "debugger_events_test.cpp",
  ]

  configs = [
    "//ark/js_runtime:ecma_test_config",
    "//ark/js_runtime:ark_jsruntime_public_config",  # should add before
                                                     # arkruntime_public_config
    "//ark/js_runtime:ark_jsruntime_common_config",
    "$ark_root/runtime:arkruntime_public_config",
  ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    "//ark/js_runtime/ecmascript/tooling:libark_ecma_debugger_test",
    sdk_libc_secshared_dep,
  ]

  if (!is_standard_system) {
    deps += [ "$ark_root/runtime:libarkruntime" ]
  }
}

host_unittest_action("DebuggerTypesTest") {
  module_out_path = module_output_path

  sources = [
    # test file
    "debugger_types_test.cpp",
  ]

  configs = [
    "//ark/js_runtime:ecma_test_config",
    "//ark/js_runtime:ark_jsruntime_public_config",  # should add before
                                                     # arkruntime_public_config
    "//ark/js_runtime:ark_jsruntime_common_config",
    "$ark_root/runtime:arkruntime_public_config",
  ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    "//ark/js_runtime/ecmascript/tooling:libark_ecma_debugger_test",
    sdk_libc_secshared_dep,
  ]

  if (!is_standard_system) {
    deps += [ "$ark_root/runtime:libarkruntime" ]
  }
}

group("unittest") {
  testonly = true

  # deps file
  deps = [
    ":DebuggerCommandsTest",
    ":DebuggerEventsTest",
    ":DebuggerTypesTest",
    ":EcmaDebugApiTest",
    ":debugger_entry",
  ]
}

group("host_unittest") {
  testonly = true

  # deps file
  deps = [
    ":DebuggerCommandsTestAction",
    ":DebuggerEventsTestAction",
    ":DebuggerTypesTestAction",
  ]
}
